#
# Copyright (C) 2022  Autodesk, Inc. All Rights Reserved.
#
# SPDX-License-Identifier: Apache-2.0
#

SET(_target
    "RV_DEPS_ATOMIC_OPS"
)

SET(_version
    ${RV_DEPS_ATOMIC_OPS_VERSION}
)

# Download a recent version that includes the feature we need (--disable-gpl) which hasn't been released nor tagged yet.
SET(_download_url
    "https://github.com/ivmai/libatomic_ops/archive/044573903530c4a8e8318e20a830d4a0531b2035.zip"
)

SET(_download_hash
    ${RV_DEPS_ATOMIC_OPS_DOWNLOAD_HASH}
)

SET(_install_dir
    ${RV_DEPS_BASE_DIR}/${_target}/install
)

SET(_lib_dir
    ${_install_dir}/lib
)

SET(_atomic_ops_lib_name
    ${CMAKE_STATIC_LIBRARY_PREFIX}atomic_ops${CMAKE_STATIC_LIBRARY_SUFFIX}
)

SET(_atomic_ops_lib
    ${_lib_dir}/${_atomic_ops_lib_name}
)

IF(RV_TARGET_WINDOWS)
  # Use CMake build on Windows (replaces autotools sh/configure/make)
  SET(_build_dir
      ${RV_DEPS_BASE_DIR}/${_target}/build
  )

  EXTERNALPROJECT_ADD(
    ${_target}
    SOURCE_DIR ${RV_DEPS_BASE_DIR}/${_target}/src
    INSTALL_DIR ${_install_dir}
    URL ${_download_url}
    URL_MD5 ${_download_hash}
    DOWNLOAD_NAME ${_target}_${_version}.zip
    DOWNLOAD_DIR ${RV_DEPS_DOWNLOAD_DIR}
    CONFIGURE_COMMAND ${CMAKE_COMMAND} -S ${RV_DEPS_BASE_DIR}/${_target}/src -B ${_build_dir}
      -DCMAKE_INSTALL_PREFIX=${_install_dir}
      -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
      -Denable_gpl=OFF
    BUILD_COMMAND ${CMAKE_COMMAND} --build ${_build_dir} --config ${CMAKE_BUILD_TYPE} -j${_cpu_count}
    INSTALL_COMMAND ${CMAKE_COMMAND} --install ${_build_dir} --prefix ${_install_dir} --config ${CMAKE_BUILD_TYPE}
    BUILD_IN_SOURCE FALSE
    BUILD_ALWAYS FALSE
    BUILD_BYPRODUCTS ${_atomic_ops_lib}
    USES_TERMINAL_BUILD TRUE
  )
ELSE()
  # Use autotools on Linux/macOS
  SET(_make_command
      make
  )
  SET(_configure_command
      sh ./configure
  )
  SET(_autogen_command
      sh ./autogen.sh
  )

  # Make sure NOT to enable GPL
  SET(_configure_args
      "--disable-gpl"
  )
  LIST(APPEND _configure_args "--prefix=${_install_dir}")

  EXTERNALPROJECT_ADD(
    ${_target}
    SOURCE_DIR ${RV_DEPS_BASE_DIR}/${_target}/src
    INSTALL_DIR ${_install_dir}
    URL ${_download_url}
    URL_MD5 ${_download_hash}
    DOWNLOAD_NAME ${_target}_${_version}.zip
    DOWNLOAD_DIR ${RV_DEPS_DOWNLOAD_DIR}
    CONFIGURE_COMMAND ${_autogen_command} && ${_configure_command} ${_configure_args}
    BUILD_COMMAND ${_make_command} -j${_cpu_count}
    INSTALL_COMMAND ${_make_command} install
    BUILD_IN_SOURCE TRUE
    BUILD_ALWAYS FALSE
    BUILD_BYPRODUCTS ${_atomic_ops_lib}
    USES_TERMINAL_BUILD TRUE
  )
ENDIF()

SET(_include_dir
    ${_install_dir}/include
)

RV_ADD_IMPORTED_LIBRARY(
  NAME atomic_ops::atomic_ops
  TYPE STATIC
  LOCATION ${_atomic_ops_lib}
  INCLUDE_DIRS ${_include_dir}
  DEPENDS ${_target}
  ADD_TO_DEPS_LIST
)

RV_STAGE_DEPENDENCY_LIBS(TARGET ${_target} OUTPUTS ${RV_STAGE_LIB_DIR}/${_atomic_ops_lib_name})

SET(RV_DEPS_ATOMIC_OPS_VERSION
    ${_version}
    CACHE INTERNAL "" FORCE
)
